#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

//@skip-check module-region-empty
#Область ПрограммныйИнтерфейс
#КонецОбласти

#Область ОбработчикиСобытий
Процедура ОбработкаЗаполнения(ДанныеЗаполнения,СтандартнаяОбработка)
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
		СтандартнаяОбработка = Ложь;
		Основание = ДанныеЗаполнения;
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	РеализацияТоваровУслуг.Дата,
			|	РеализацияТоваровУслуг.Склад,
			|	РеализацияТоваровУслуг.Контрагент,
			|	РеализацияТоваровУслуг.СмещениеВремениСклада
			|ИЗ
			|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
			|ГДЕ
			|	РеализацияТоваровУслуг.Ссылка = &Ссылка
			|	И РеализацияТоваровУслуг.Проведен
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	РеализацияТоваровУслугСписокНоменклатуры.Номенклатура,
			|	РеализацияТоваровУслугСписокНоменклатуры.Количество,
			|	РеализацияТоваровУслугСписокНоменклатуры.Цена,
			|	РеализацияТоваровУслугСписокНоменклатуры.Сумма
			|ИЗ
			|	Документ.РеализацияТоваровУслуг.СписокНоменклатуры КАК РеализацияТоваровУслугСписокНоменклатуры
			|ГДЕ
			|	РеализацияТоваровУслугСписокНоменклатуры.Ссылка = &Ссылка
			|	И РеализацияТоваровУслугСписокНоменклатуры.Номенклатура.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Товар)";
		Запрос.Параметры.Вставить("Ссылка", ДанныеЗаполнения);
		
		РезультатПакет = Запрос.ВыполнитьПакет();
		
		ВыборкаШапка = РезультатПакет[0].Выбрать();
		ВыборкаНоменклатура = РезультатПакет[1].Выбрать();
		
		Если ВыборкаШапка.Следующий() Тогда
			Если ВыборкаНоменклатура.Количество() = 0 Тогда
				ВызватьИсключение НСтр("ru = 'В документе-основании отсутствуют товары'");
			КонецЕсли;
			ЗаполнитьЗначенияСвойств(ЭтотОбъект, ВыборкаШапка, "Дата, Склад, Контрагент, СмещениеВремениСклада");				
		Иначе
			ВызватьИсключение НСтр("ru = 'Отсутствует проведённый документ-основание'");
		КонецЕсли;
		
		Пока ВыборкаНоменклатура.Следующий() Цикл
			НоваяСтрока = СписокНоменклатуры.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаНоменклатура);			
		КонецЦикла;
	Иначе
		ВызватьИсключение НСтр("ru='Документ можно вводить только на основании Реализации товаров и услуг'");		 
	КонецЕсли;
КонецПроцедуры

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Префикс = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Склад, "Префикс");
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	МетодСписанияСебестоимости = гваУчетнаяПолитика.МетодСписанияСебестоимости(Дата);
	Если МетодСписанияСебестоимости = Неопределено Тогда
		ТекстСообщения = НСтр("ru='Не указан метод списания себестоимости в учетной политике'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , , , Отказ);
		Возврат;
	КонецЕсли;
	
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	Движения.Записать();
	Движения.ОстаткиНоменклатуры.Записывать = Истина;	
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ОстаткиНоменклатуры");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Склад", Склад);
	ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	Блокировка.Заблокировать();
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
	|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество
	|ПОМЕСТИТЬ втТЧТовары
	|ИЗ
	|	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
	|ГДЕ
	|	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
	|СГРУППИРОВАТЬ ПО
	|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втТЧТовары.Номенклатура КАК Номенклатура,
	|	втТЧТовары.Номенклатура.Представление КАК НоменклатураПредставление,
	|	втТЧТовары.Количество КАК Количество,
	|	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
	|	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.СтоимостьОстаток, 0) КАК СтоимостьОстаток,
	|	ОстаткиНоменклатурыОстатки.Партия КАК Партия
	|ИЗ
	|	втТЧТовары КАК втТЧТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиНоменклатуры.Остатки(&МоментВремени, Номенклатура В
	|			(ВЫБРАТЬ
	|				втТЧТовары.Номенклатура КАК Номенклатура
	|			ИЗ
	|				втТЧТовары КАК втТЧТовары)
	|		И Склад = &Склад) КАК ОстаткиНоменклатурыОстатки
	|		ПО втТЧТовары.Номенклатура = ОстаткиНоменклатурыОстатки.Номенклатура
	|
	|УПОРЯДОЧИТЬ ПО
	|	ОстаткиНоменклатурыОстатки.Партия.МоментВремени
	|ИТОГИ
	|	МАКСИМУМ(Количество),
	|	СУММА(КоличествоОстаток)
	|ПО
	|	Номенклатура";
	
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Склад", Склад);
	
	ВыборкаНоменклатура = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаНоменклатура.Следующий() Цикл
		Если ВыборкаНоменклатура.Количество > ВыборкаНоменклатура.КоличествоОстаток Тогда
			ТекстСообщения = СтрШаблон(НСтр("ru='Недостаточно товара %1 в количестве %2'"), 
													ВыборкаНоменклатура.НоменклатураПредставление, 
													ВыборкаНоменклатура.Количество - ВыборкаНоменклатура.КоличествоОстаток
											) ;
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , , , Отказ);
		КонецЕсли;
		Если Отказ Тогда
			Продолжить;
		КонецЕсли;
		
		КоличествоСписать = ВыборкаНоменклатура.Количество;
		ВыборкаДетальныеЗаписи = ВыборкаНоменклатура.Выбрать();
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			Количество = Мин(КоличествоСписать, ВыборкаДетальныеЗаписи.КоличествоОстаток);
			Если Количество = ВыборкаДетальныеЗаписи.КоличествоОстаток Тогда
				Себестоимость = ВыборкаДетальныеЗаписи.СтоимостьОстаток;
			Иначе
				Себестоимость = Количество / ВыборкаДетальныеЗаписи.КоличествоОстаток * ВыборкаДетальныеЗаписи.СтоимостьОстаток;
			КонецЕсли;
			
			Движение = Движения.ОстаткиНоменклатуры.ДобавитьРасход();
			Движение.Период = Дата;
			Движение.Номенклатура = ВыборкаДетальныеЗаписи.Номенклатура;
			Движение.Партия = ВыборкаДетальныеЗаписи.Партия;
			Движение.Количество = Количество;
			Движение.Стоимость = Себестоимость;
			Движение.ПериодФилиал = Дата + СмещениеВремениСклада;
			Движение.Склад = Склад; 
			
			КоличествоСписать = КоличествоСписать - Количество;
			Если КоличествоСписать <= 0 Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти

//@skip-check module-region-empty
#Область СлужебныйПрограммныйИнтерфейс
#КонецОбласти

//@skip-check module-region-empty
#Область СлужебныеПроцедурыИФункции
#КонецОбласти
#КонецЕсли